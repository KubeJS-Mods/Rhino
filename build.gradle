plugins {
    id 'forgified-fabric-loom' version '0.6-SNAPSHOT'
    id "com.matthewprenger.cursegradle" version "1.4.0"
    id 'maven-publish'
}

apply from: 'https://files.latmod.com/public/markdown-git-changelog.gradle'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

def ENV = System.getenv()
version = "${mod_version}-build.${ENV.BUILD_NUMBER ?: 9999}"
archivesBaseName = project.archives_base_name
group = project.maven_group

loom {
    silentMojangMappingsLicense()

    mixinConfigs = []

    useFabricMixin = true

    dataGen {
        mod project.mod_id
    }

    launches {
        data {
            arg "--existing", file("src/main/resources").absolutePath
        }
    }
}

repositories {
    maven { url "https://www.cursemaven.com" }
    maven { url "https://maven.shedaniel.me/" }
    maven {
        url "https://maven.saps.dev/minecraft"
        content {
            includeGroup "dev.ftb.mods"
            includeGroup "dev.latvian.mods"
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings minecraft.officialMojangMappings()
    forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    def targetVersion = 8
    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = targetVersion
    }
}

java {
    // withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }

    manifest {
        attributes([
                "Specification-Title"     : project.mod_id,
                "Specification-Vendor"    : project.mod_author,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : project.mod_author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }

    classifier null
}

if (ENV.CURSEFORGE_KEY) {
    curseforge {
        apiKey = ENV.CURSEFORGE_KEY
        project {
            id = "${curseforge_id}"
            releaseType = "release"
            addGameVersion "1.14.4"
            addGameVersion "1.15.2"
            addGameVersion "1.16.1"
            addGameVersion "1.16.2"
            addGameVersion "1.16.3"
            addGameVersion "1.16.4"
            addGameVersion "1.16.5"
            addGameVersion "Forge"
            addGameVersion "Fabric"
            mainArtifact(remapJar.archivePath)
            changelog = getGitChangelog
            changelogType = 'markdown'
        }
    }
}

publishing {
    publications {
        mavenRhino(MavenPublication) {
            artifactId = project.archives_base_name
            artifact(remapJar) {
                builtBy remapJar
            }
        }
    }
    repositories {
        if (ENV.LOCAL_MAVEN) {
            maven {
                url = "file://${ENV.LOCAL_MAVEN}"
            }
        }

        if (ENV.SAPS_TOKEN) {
            maven {
                url "https://maven.saps.dev/minecraft"
                credentials {
                    username = "latvian"
                    password = "${ENV.SAPS_TOKEN}"
                }
            }
        }
    }
}
